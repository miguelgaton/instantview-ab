# Declaramos que versión usamos de instant view.
~version: "2.1"
# Definimos que urls usan la plantilla y cuales no.
?path: /.+
!path_not: /
!path_not: /category/
!path_not: /tag/
!path_not: /coches/
!path_not: /category/.+
!path_not: /tag/.+
!path_not: /coches/.+
# ¿Existe la etiqueta articulo y contiene la clase type-post?
?exists: //article[has-class("type-post")]
# Arreglo articulos relacionados.
$rel_url: //a[@class="post-relacionado"]/@href
$rel_tag: //a[@class="post-relacionado"]
@set_attr(href, $rel_url): //div[@class="post-relacionado-title"]
@replace_tag(<a>): //div[@class="post-relacionado-title"]
# Eliminamos elementos.
@remove: //div//a[@class="site-logo"]
#@remove: //p[.//img]
#@remove: //a[.//img]
#@remove: //p[.//aside]
@remove: //div[@id="abn-shares-buttons"]
@remove: //div[@id="toc_container"]
@remove: //noscript//img
@remove: //aside[has-class("publi")]
# Eliminamos las imagenes en las tableheader de las comparativas (no soportado)
@remove: //th//a
# Reemplazamos ciertos tags anidados que la función simplify no soporta.
@replace_tag(<div>): //a[.//img]
@replace_tag(<div>): //h3[.//img]
@replace_tag(<div>): //p[.//aside]
@replace_tag(<div>): //p[.//img]
@replace_tag(<div>): //p[.//iframe]
#@replace_tag(<div>): //th[.//img]
# Twitter tweets pasan a P
@replace_tag(<p>): //blockquote
# Reemplazamos el atributo src por data-lazy-src ya que el contenido de src por defecto no se soporta.
@set_attr(src, @data-lazy-src): //img
# Definimos las partes del articulo básicas para la generación del IV.
title: //h1[has-class("post-title")]
body: //div[@class="post-content"]
# Gestión googleplay.
@if (//div[has-class("googleplay")]) {
  @remove: //div[has-class("qrcode")]
  @remove: //div[has-class("applinks")]
  #@remove: //div[has-class("appicon")]
  @remove: //div[has-class("screenshots")]
}
# Gestión dailymotion
@remove: //iframe[contains(@data-lazy-src, "www.dailymotion.com")]
@remove: //iframe[contains(@src, "www.dailymotion.com")]
# Death to all iframes.
@remove: //iframe